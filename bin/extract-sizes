#! /bin/sh
# extracts the coverage report from
# $ forge coverage --report lcov
# and reformats it to contain minimal information
# The output should be stored in git - that's your job :-)

# echo $0 "run on" `date`

# grep '^|.*|$'

awk  -v cols=1 -v width=10 -v total_width=60 '
BEGIN {
  FS="|"
  # Define the format string with variable widths
  name_width = total_width - (cols * (width + 1)) - 4
  value_width = width - 2 # 2 spaces either size
}

# Header
/^\| Contract +\|/ {
  printf "╭%s┬%s╮\n", chars("─", name_width + 2), chars("─", value_width + 2)
  printf "│ %-" name_width "s │ %-" value_width "s │\n", "contract", "bytes"
  printf "├%s┼%s┤\n", chars("─", name_width + 2), chars("─", value_width + 2)
}

# Data line
/^\| [0-9a-zA-Z_$]+ +\| +[0-9,]+ \|/ {
    gsub(/^ +| +$/, "", $3)
    printf "│ %-" name_width "s │ %" value_width "s │\n", $2, $3
}

END {
  printf "╰%s┴%s╯\n", chars("─", name_width + 2), chars("─", value_width + 2)
}

# Function to repeat a character n times
function chars(char, count) {
    result = ""
    for (i = 0; i < count; i++) {
        result = result char
    }
    return result
}'